import java.util.Properties

plugins {
    alias(libs.plugins.androidLibrary)
    alias(libs.plugins.hiltAndroid)
    alias(libs.plugins.ksp)
    alias(libs.plugins.composeCompiler)
}

android {
    namespace = "com.dawncourse.feature.update"
    compileSdk = 37

    defaultConfig {
        minSdk = 26
        consumerProguardFiles("proguard-rules.pro")
        val sourceConfig = Properties().apply {
            rootProject.file("config/qlu-update.properties").takeIf { it.isFile }?.inputStream()?.use { load(it) }
        }
        val sourceUrl = providers.gradleProperty("qlu.updateUrl").orElse(sourceConfig.getProperty("metadataUrl", "")).get().trim()
        require(sourceUrl.none { it == '\n' || it == '\r' }) { "Update source must be one URL" }
        buildConfigField("String", "DEFAULT_UPDATE_URL", "\"" + sourceUrl.replace("\\", "\\\\").replace("\"", "\\\"") + "\"")
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    // kotlinOptions 块已移除：built-in Kotlin 下 jvmTarget 默认等于
    // 上面的 compileOptions.targetCompatibility，无需重复声明
}

dependencies {
    implementation(project(":core:domain"))
    implementation(project(":core:ui"))

    implementation(libs.core.ktx)
    implementation(libs.hilt.android)
    implementation(libs.hilt.navigation.compose)
    ksp(libs.hilt.compiler)

    implementation(platform(libs.compose.bom))
    implementation(libs.ui)
    implementation(libs.ui.graphics)
    implementation(libs.ui.tooling.preview)
    implementation(libs.material3)
    implementation(libs.material.icons.extended)

    implementation(libs.lifecycle.runtime.ktx)
    implementation(libs.retrofit)
    implementation(libs.converter.gson)
    implementation(libs.okhttp)
    implementation(libs.logging.interceptor)

    // 单元测试（纯 JVM 测试）
    testImplementation(libs.junit)
    // 端到端复现 issue #115：需要真实 OkHttpClient + TLS 服务端，而非仅业务层 mock
    testImplementation(libs.mockwebserver)
    testImplementation(libs.okhttp.tls)
}
